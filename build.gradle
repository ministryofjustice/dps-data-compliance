import org.springframework.boot.gradle.plugin.SpringBootPlugin

import java.time.Instant
import java.time.LocalDate

plugins {
    id 'java'
    id 'org.springframework.boot' version '2.2.6.RELEASE' // consider removing pinned dependencies when updating
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'org.owasp.dependencycheck' version '5.3.2.1'
    id 'se.patrikerdes.use-latest-versions' version '0.2.13'
}

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    agentDeps
    agentDeps.transitive = false
}

dependencyCheck {
    failBuildOnCVSS = 5
    suppressionFiles = ['dependency-check-suppress-spring.xml']
    format = 'ALL'
    analyzers {
        assemblyEnabled = false
    }
}

project.sourceCompatibility = 11
project.targetCompatibility = 11

group = 'uk.gov.justice.hmpps'

def today = Instant.now()
def todaysDate = LocalDate.now().format('yyyy-MM-dd')
version = System.getenv('CI') ? "${todaysDate}.${System.getenv('CIRCLE_BUILD_NUM')}" : todaysDate

springBoot {
    buildInfo {
        properties {
            artifact = rootProject.name
            version
            group
            name = rootProject.name
            time = today
            additional = [
                    by                   : System.properties['user.name'],
                    operatingSystem      : "${System.properties['os.name']} (${System.properties['os.version']})",
                    continuousIntegration: System.getenv('CI') ? true : false,
                    machine              : InetAddress.localHost.hostName
            ]
        }
    }
}

dependencyManagement {
    imports {
        mavenBom(SpringBootPlugin.BOM_COORDINATES)
    }
}

ext ['spring-security.version'] = '5.3.0.RELEASE' // Updated since spring-boot-starter-oauth2-resource-server-2.2.6.RELEASE only pulls in 5.2.2.RELEASE (still affected by CVE-2018-1258 though)

dependencies {

    // Lombok
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'

    // Spring
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-quartz'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-reactor-netty'
    implementation 'org.springframework:spring-webflux:5.2.5.RELEASE'
    implementation 'org.springframework:spring-jms:5.2.5.RELEASE'

    implementation 'com.github.timpeeters:spring-boot-graceful-shutdown:2.2.1'

    // Logging
    implementation 'net.logstash.logback:logstash-logback-encoder:6.3'
    implementation 'com.microsoft.azure:applicationinsights-spring-boot-starter:2.6.0'
    implementation 'com.microsoft.azure:applicationinsights-logging-logback:2.6.0'
    agentDeps 'com.microsoft.azure:applicationinsights-agent:2.6.0'
    implementation configurations.agentDeps

    // AWS
    implementation platform('software.amazon.awssdk:bom:2.11.10')
    implementation 'software.amazon.awssdk:rekognition'

    implementation platform('com.amazonaws:aws-java-sdk-bom:1.11.760')
    implementation 'com.amazonaws:aws-java-sdk-sns:1.11.760'
    implementation 'com.amazonaws:amazon-sqs-java-messaging-lib:1.0.8'

    // Utils
    implementation 'io.github.resilience4j:resilience4j-retry:1.3.1'
    implementation 'com.google.guava:guava:28.2-jre'
    implementation 'org.apache.commons:commons-lang3:3.10'
    implementation 'commons-io:commons-io:2.6'
    implementation ('org.quartz-scheduler:quartz:2.3.2') {
        exclude group: 'com.zaxxer'
    }

    implementation 'com.nimbusds:nimbus-jose-jwt:8.14' // pinned due to CVE-2019-17195 in 7.8.1 brought in by spring-boot-starter-oauth2-resource-server-2.2.6.RELEASE

    // Database
    implementation 'org.hibernate:hibernate-core:5.4.13.Final'

    runtimeOnly 'org.hsqldb:hsqldb:2.5.0'
    runtimeOnly 'org.flywaydb:flyway-core:6.3.3'
    runtimeOnly 'org.postgresql:postgresql:42.2.12'

    // Swagger
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'

    // Test
    testAnnotationProcessor "org.projectlombok:lombok:1.18.12"
    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    testImplementation("org.junit.jupiter:junit-jupiter:5.6.1")
    testImplementation 'org.assertj:assertj-core:3.15.0'
    testImplementation 'net.javacrumbs.json-unit:json-unit-assertj:2.17.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'com.squareup.okhttp3:okhttp:4.5.0'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.5.0'
    testImplementation 'io.jsonwebtoken:jjwt:0.9.1'
}

task copyAgent(type: Copy) {
    from configurations.agentDeps
    into "$buildDir/libs"
}

assemble.dependsOn copyAgent

test {
    useJUnitPlatform()
}
