import org.springframework.boot.gradle.plugin.SpringBootPlugin

import java.time.Instant
import java.time.LocalDate

plugins {
    id 'java'
    id 'org.springframework.boot' version '2.2.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'org.owasp.dependencycheck' version '5.3.0'
}

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    agentDeps
    agentDeps.transitive = false
}

dependencyCheck {
    failBuildOnCVSS = 5
    suppressionFiles = ['dependency-check-suppress-spring.xml']
    format = 'ALL'
    analyzers {
        assemblyEnabled = false
    }
}

project.sourceCompatibility = 11
project.targetCompatibility = 11

group = 'uk.gov.justice.hmpps'

def today = Instant.now()
def todaysDate = LocalDate.now().format('yyyy-MM-dd')
version = System.getenv('CI') ? "${todaysDate}.${System.getenv('CIRCLE_BUILD_NUM')}" : todaysDate

springBoot {
    buildInfo {
        properties {
            artifact = rootProject.name
            version
            group
            name = rootProject.name
            time = today
            additional = [
                    by                   : System.properties['user.name'],
                    operatingSystem      : "${System.properties['os.name']} (${System.properties['os.version']})",
                    continuousIntegration: System.getenv('CI') ? true : false,
                    machine              : InetAddress.localHost.hostName
            ]
        }
    }
}

dependencyManagement {
    imports {
        mavenBom(SpringBootPlugin.BOM_COORDINATES)
    }
}

dependencies {

    // Lombok
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'

    // Spring
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-quartz'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-reactor-netty'
    implementation 'org.springframework:spring-webflux:5.2.3.RELEASE'
    implementation 'org.springframework:spring-jms:5.2.4.RELEASE'

    // Logging
    implementation 'net.logstash.logback:logstash-logback-encoder:6.3'
    implementation 'com.microsoft.azure:applicationinsights-spring-boot-starter:2.5.1'
    implementation 'com.microsoft.azure:applicationinsights-logging-logback:2.5.1'
    agentDeps 'com.microsoft.azure:applicationinsights-agent:2.5.1'
    implementation configurations.agentDeps

    // AWS
    implementation platform('software.amazon.awssdk:bom:2.10.74')
    implementation 'software.amazon.awssdk:rekognition'

    implementation platform('com.amazonaws:aws-java-sdk-bom:1.11.732')
    implementation 'com.amazonaws:aws-java-sdk-sns:1.11.732'
    implementation 'com.amazonaws:amazon-sqs-java-messaging-lib:1.0.8'

    // Utils
    implementation 'com.google.guava:guava:28.2-jre'
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'commons-io:commons-io:2.6'
    implementation ('org.quartz-scheduler:quartz:2.3.2') {
        exclude group: 'com.zaxxer'
    }

    // Database
    implementation 'org.hibernate:hibernate-core:5.4.10.Final'

    runtime 'org.hsqldb:hsqldb:2.5.0'
    runtime 'org.flywaydb:flyway-core:6.2.4'
    runtime 'org.postgresql:postgresql:42.2.10'

    // Test
    testAnnotationProcessor "org.projectlombok:lombok"
    testCompileOnly 'org.projectlombok:lombok'
    testImplementation("org.junit.jupiter:junit-jupiter:5.6.0")
    testImplementation 'org.assertj:assertj-core:3.15.0'
    testImplementation 'net.javacrumbs.json-unit:json-unit-assertj:2.13.1'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'com.squareup.okhttp3:okhttp:4.4.0'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.4.0'
}

task copyAgent(type: Copy) {
    from configurations.agentDeps
    into "$buildDir/libs"
}

assemble.dependsOn copyAgent

test {
    useJUnitPlatform()
}
