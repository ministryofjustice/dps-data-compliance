import org.springframework.boot.gradle.plugin.SpringBootPlugin

import java.time.Instant
import java.time.LocalDate

plugins {
    id 'java'
    id 'org.springframework.boot' version '2.2.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'com.github.ben-manes.versions' version '0.27.0'
}

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    agentDeps
    agentDeps.transitive = false
}

project.sourceCompatibility = '11'
project.targetCompatibility = '11'

group = 'uk.gov.justice.hmpps'

def today = Instant.now()
def todaysDate = LocalDate.now().format('yyyy-MM-dd')
version = System.getenv('CI') ? "${todaysDate}.${System.getenv('CIRCLE_BUILD_NUM')}" : todaysDate

jar {
    archiveBaseName.set(rootProject.name)

    manifest {
        attributes 'Implementation-Title': rootProject.name, 'Implementation-Version': archiveVersion.get()
    }
}

springBoot {
    buildInfo {
        properties {
            artifact = rootProject.name
            version = version
            group = group
            name = rootProject.name
            time = today
            additional = [
                    by                   : System.properties['user.name'],
                    operatingSystem      : "${System.properties['os.name']} (${System.properties['os.version']})",
                    continuousIntegration: System.getenv('CI') ? true : false,
                    machine              : InetAddress.localHost.hostName
            ]
        }
    }
}

dependencyManagement {
    imports {
        mavenBom(SpringBootPlugin.BOM_COORDINATES)
    }
}

dependencies {

    annotationProcessor 'org.projectlombok:lombok'

    compileOnly 'org.projectlombok:lombok'

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'net.logstash.logback:logstash-logback-encoder:6.2'
    implementation 'com.microsoft.azure:applicationinsights-spring-boot-starter:2.5.1'
    implementation 'com.microsoft.azure:applicationinsights-logging-logback:2.5.1'
    agentDeps 'com.microsoft.azure:applicationinsights-agent:2.5.1'
    implementation configurations.agentDeps

    implementation 'software.amazon.awssdk:aws-sdk-java:2.10.0'
    implementation 'software.amazon.awssdk:sns:2.10.0'

    implementation 'org.apache.commons:commons-lang3:3.9'

    testRuntime('org.junit.jupiter:junit-jupiter-engine:5.4.2')
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.4.2')
    testImplementation("org.assertj:assertj-core:3.6.1")
    testImplementation("org.springframework.boot:spring-boot-starter-test:2.2.1.RELEASE")
}

test {
    useJUnitPlatform()
}
